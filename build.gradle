buildscript {
    ext.kotlin_version = "1.6.10"
    ext.kotlin_reflect_version = "1.6.0"

    repositories {
        maven { url = "https://maven.minecraftforge.net" }
        maven { url = "https://repo.spongepowered.org/repository/maven-public/" }
        maven { url = "https://plugins.gradle.org/m2/" }
        maven { url = "https://maven.parchmentmc.org" }
        mavenCentral()
    }
    dependencies {
        classpath group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "5.1.+", changing: true
        classpath "org.spongepowered:mixingradle:0.7-SNAPSHOT"
        classpath group: "com.github.jengelman.gradle.plugins", name: "shadow", version: "6.1.0"
        classpath group: "org.jetbrains.kotlin", name: "kotlin-gradle-plugin", version: kotlin_version
        classpath group: "org.parchmentmc", name: "librarian", version: "1.+"
    }
}

apply plugin: "net.minecraftforge.gradle"
apply plugin: "org.spongepowered.mixin"
apply plugin: "org.parchmentmc.librarian.forgegradle"
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "kotlin"

apply from: 'https://raw.githubusercontent.com/thedarkcolour/KotlinForForge/site/thedarkcolour/kotlinforforge/gradle/kff-3.1.0.gradle'

group = "dev.psygamer"
version = "1.0"

java {
    archivesBaseName = "gunmod"
    toolchain.languageVersion = JavaLanguageVersion.of(17)
}

minecraft {
    mappings channel: "parchment", version: "2022.02.13-1.18.1"

    runs {
        client {
            workingDirectory project.file("run")

            property "forge.logging.markers", "REGISTRIES"
            property "forge.logging.console.level", "debug"

            mods {
                gunmod {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file("run")

            property "forge.logging.markers", "REGISTRIES"
            property "forge.logging.console.level", "debug"

            mods {
                gunmod {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file("run")

            property "forge.logging.markers", "REGISTRIES"
            property "forge.logging.console.level", "debug"

            args "--mod", "gunmod", "--all",
                    "--output", file("src/generated/resources/"),
                    "--existing", file("src/main/resources/")

            mods {
                gunmod {
                    source sourceSets.main
                }
            }
        }
    }
}

mixin {
    add sourceSets.main, "gunmod.refmap.json"

    config "gunmod.mixins.json"
}

sourceSets.main.resources { srcDir "src/generated/resources" }

configurations {
    shadow

    compile.extendsFrom shadow
}

repositories {
    maven { url = "https://thedarkcolour.github.io/KotlinForForge/" }
}

dependencies {
    minecraft "net.minecraftforge:forge:1.18.1-39.0.79"
    annotationProcessor "org.spongepowered:mixin:0.8.5:processor"

    implementation "thedarkcolour:kotlinforforge:3.0.0"
}

shadowJar {
    configurations = [project.configurations.shadow]

    classifier ""
    finalizedBy "reobfJar"

    manifest {
        attributes([
                "Specification-Title"     : "gunmod",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.version,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

jar {
    classifier "standalone"
    finalizedBy "reobfJar"

    manifest {
        attributes([
                "Specification-Title"     : "gunmod",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.version,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

reobf {
    shadowJar {}
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "17"
    }
}
